!read topology and parameter
stream ../../toppar.str

read psf card name ../../step1_pdbreader.psf
read coor card name ../../step1_pdbreader.crd

!open unit 51 read unform name md500_protein_skip100_nopbc-fit.dcd
open unit 51 read unform name ../simulation_3/trajectory_0-1000_nopbc-protein-fit.dcd

!---------------------------------
! specify how we are going to read the trajectory
!---------------------------------
TRAJectory QUER UNIT 51
set tstrt ?START
calc tend = @tstrt + ?NSTEP
traj first 51 nunit 1 begin @tstrt stop @tend
set frame @tstrt

!------------------------
!set output file name
!-----------------------
open write unit 25 card name interaction-energy_elec_1.txt
write title unit 25
* # electrostatic interaction energy between peptide(PROB) and protein(PROA)
* # frame 1 2 3 4 5 6
*

open write unit 26 card name interaction-energy_vdw_1.txt
write title unit 26
* # vdw interaction energy between peptide(PROB) and protein(PROA)
* # frame 1 2 3 4 5 6
*

open write unit 27 card name interaction-energy_total_1.txt
write title unit 27
* # total interaction energy between peptide(PROB) and protein(PROA)
* # frame 1 2 3 4 5 6
*

open write unit 28 card name interaction-energy_elec_2.txt
write title unit 28
* # electrostatic interaction energy between peptide(PROB) and protein(PROA)
* # 7 8 9 10 11 12 13
*

open write unit 29 card name interaction-energy_vdw_2.txt
write title unit 29
* # vdw interaction energy between peptide(PROB) and protein(PROA)
* # 7 8 9 10 11 12 13
*

open write unit 30 card name interaction-energy_total_2.txt
write title unit 30
* # total interaction energy between peptide(PROB) and protein(PROA)
* # 7 8 9 10 11 12 13
*
!-----------------
!loop starts
!-----------------
label trajloop


!---------------------
!read trajectory frame and
!calculate properties
!--------------------

traj read
! put cutoff options here
update cutnb 12.0 ctofnb 12.0 fshift vshift cdie

ener

set resid 1
label loop
calc residue = @resid

inte sele segid PROA end sele segid PROB .and resid @residue end

calc @resid-e = ?ELEC
calc @resid-v = ?VDW
calc @resid-t =  ?ELEC + ?VDW

incr resid by 1
if @resid .le. 13 goto loop

!-------------------------
!write the parameters to the file
!-------------------------
write title unit 25
* @frame @{1-e} @{2-e} @{3-e} @{4-e} @{5-e} @{6-e}
*

write title unit 26
* @frame @{1-v} @{2-v} @{3-v} @{4-v} @{5-v} @{6-v}
*

write title unit 27
* @frame @{1-t} @{2-t} @{3-t} @{4-t} @{5-t} @{6-t}
*

write title unit 28
* @{7-e} @{8-e} @{9-e} @{10-e} @{11-e} @{12-e} @{13-e}
*

write title unit 29
* @{7-v} @{8-v} @{9-v} @{10-v} @{11-v} @{12-v} @{13-v}
*

write title unit 30
* @{7-t} @{8-t} @{9-t} @{10-t} @{11-t} @{12-t} @{13-t}
*

!-------------------------
!goto the next frame
!------------------
incr frame  by 5
if @frame .lt. @tend  goto trajloop

stop
